* constraints
*
OPEN UNIT 3 NAME @d/@input.reg READ FORM
READ  COOR COMP CARD UNIT 3
CLOSE UNIT 3


! atoms beyond 18A will be held fixed, so only need to add constraints to
! buffer region atoms within that cutoff
!set 1 20.0    ! radius for fully mobile region
!set 2 24.0    ! radius of inner region
!set 3 22.0    ! radius of buffer region where Langevin dynamics will be used

! Setting various boundary flags
scalar xcomp store 1    ! initial md region
scalar ycomp store 2    ! initial buffer region
scalar zcomp store 3    ! initial Langevin atoms-proteins only
scalar wcomp store 4    ! reservoir region atoms

! Convert crystallographic thermal factors in wmain to constraint forces
scalar wmain reci sele recall 3 end               ! get 1/B
scalar wmain mult 0.001987191 sele recall 3 end   ! k(boltz)/B
scalar wmain mult @temp sele recall 3 end         ! kT/B
scalar wmain mult 4.0 sele recall 3 end           ! 4kT/B
scalar wmain mult 9.87 sele recall 3 end          ! 4(pi**2)kT/B
scalar wmain set 0.0 sele .not. recall 3 end      ! zero out the rest
scalar wmain store 5

scalar xcomp set 0.0 sele all end
scalar ycomp set 0.0 sele all end
scalar zcomp set 0.0 sele all end
scalar wcomp set 0.0 sele all end

scalar xcomp recall 3                    ! langevin region constraint flags
scalar zcomp set 1.0 sele recall 3 .or. (resn TIP3 .and. type OH2) end
scalar zcomp mult 80.0 sele recall 3 end                     ! protein friction
scalar zcomp mult 80.0 sele (resn TIP3 .and. type OH2) end   ! TIP3 friction
scalar ycomp recall 5                    ! unscaled constraint force constants
scalar wcomp recall 1

open unit 15 write form name  @d/@output.cnu
write coor comp card unit 15
* col. 1: langevin region constraint flags
* col. 2: UNSCALED langevin region harmonic constraint forces
* col. 3: langevin friction coefficients.
* col. 4: MD region flags.
*
! ****** Scale the constraints based on where the atoms are ******
! Values of S(r) for scaling the constraint forces

set 4 0.00 !inner langevin region
set 5 0.08 !next
set 6 0.20 !next
set 7 0.32 !next
set 8 0.44 !next
set 9 0.50 !next

!calc zones for scaling
calc lang = @rsphere - @rdroff
calc lang4 = @rdroff 
calc lang5 = @rdroff + 0.5
calc lang6 = @rdroff + 1.0
calc lang7 = @rdroff + 1.5
calc lang8 = @rdroff + 2.0

!force constant scaling
scalar xcomp store 1
scalar ycomp mult @4 sele ( recall 1 .and. point @xcen @ycen @zcen cut @lang4 ) end
scalar ycomp mult @5 sele ( recall 1 .and. point @xcen @ycen @zcen cut @lang5 -
                            .and. .not. point @xcen @ycen @zcen cut @lang4 ) end
scalar ycomp mult @6 sele ( recall 1 .and. point @xcen @ycen @zcen cut @lang6 -
                            .and. .not. point @xcen @ycen @zcen cut @lang5 ) end
scalar ycomp mult @7 sele ( recall 1 .and. point @xcen @ycen @zcen cut @lang7 -
                            .and. .not. point @xcen @ycen @zcen cut @lang6 ) end
scalar ycomp mult @8 sele ( recall 1 .and. point @xcen @ycen @zcen cut @lang8 -
                            .and. .not. point @xcen @ycen @zcen cut @lang7 ) end
scalar ycomp mult @9 sele ( recall 1 .and. .not. point @xcen @ycen @zcen cut @lang8 ) end

!friction scaling
scalar zcomp mult @4 sele (recall 1 .and. point @xcen @ycen @zcen cut @lang4 -
       .and. .not. resn TIP3) end
scalar zcomp mult @5 sele (recall 1 .and. point @xcen @ycen @zcen cut @lang5 -
       .and. .not. (point @xcen @ycen @zcen cut @lang4 .or. resn TIP3)) end
scalar zcomp mult @6 sele (recall 1 .and. point @xcen @ycen @zcen cut @lang6 -
       .and. .not. (point @xcen @ycen @zcen cut @lang5 .or. resn TIP3)) end
scalar zcomp mult @7 sele (recall 1 .and. point @xcen @ycen @zcen cut @lang7 -
       .and. .not. (point @xcen @ycen @zcen cut @lang6 .or. resn TIP3)) end
scalar zcomp mult @8 sele (recall 1 .and. point @xcen @ycen @zcen cut @lang8 -
       .and. .not. (point @xcen @ycen @zcen cut @lang7 .or. resn TIP3)) end
scalar zcomp mult @9 sele (recall 1 .and. .not. -
       (point @xcen @ycen @zcen cut @lang8 .or. resn TIP3)) end

open unit 14 write form name @d/@output.cns
write coor comp card unit 14
* col. 1: langevin region constraint flags
* col. 2: SCALED langevin region harmonic constraint forces
* col. 3: SCALED langevin friction coefficients.
* col. 4: MD region flags.

stop